dnl @configure_input@
dnl

AC_PREREQ([2.68])
AC_INIT([Vicare/Expat],[0.4d1],[marco.maggi-ipsu@poste.it],
  [vicare-expat],[http://marcomaggi.github.io/vicare.html])
AC_CONFIG_SRCDIR([src])
AC_CONFIG_MACRO_DIR([meta/autotools])
AC_CONFIG_AUX_DIR([meta/autotools])
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE([foreign subdir-objects dist-xz no-dist-gzip])
AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_CC_C_O
AM_PROG_AR
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_MKDIR_P
LT_PREREQ([2.4])
LT_INIT([shared disable-static])
AC_LANG([C])
AC_DEFINE([_GNU_SOURCE],[1],[enable everything GNU])

VICARE_SCHEME
VICARE_CHECK_LIBRARY([VICARE],[(vicare (0 4 2017 1 (>= 10)))])
AS_IF([test "$vicare_cv_schemelib_VICARE" = no],
  [AC_MSG_ERROR([wrong Vicare version],1)])

VICARE_CHECK_LIBRARY([VICARE_FFI],
  [(vicare ffi (or (0 4 2015 5 (>= 27))
                   (0 4 2015 (>= 6))
                   (0 4 (>= 2016))))])

AS_IF([test "$vicare_cv_schemelib_VICARE_FFI" = no],
  [AC_MSG_ERROR([wrong Vicare FFI library version],1)])

# Command  line  options  for  the executable  "vicare".   The  variable
# AX_VFLAGS is  for options preselected by  the building infrastructure.
# The variable VFLAGS  is for user options selected on  the command line
# of "configure" and "make", for example:
#
#    $ make VFLAGS="-g --option print-loaded-libraries"
#
AS_VAR_SET(AX_VFLAGS,["-O2 --verbose"])
AS_VAR_SET_IF(VFLAGS,,[AS_VAR_SET(VFLAGS)])

VICARE_OPTION_DEBUGGING_MODE

AM_CONDITIONAL([WANT_VICARE_DEBUG],[test x$VICARE_DEBUG = xyes])
AM_COND_IF([WANT_VICARE_DEBUG],[AS_VAR_SET(VFLAGS,"$VFLAGS -g")])

VICARE_ENABLE_OPTION([INSTALL_SOURCES],[sources-installation],[no],
  [whether to install source Scheme libraries],
  [enable installation of source Scheme libraries])
AM_CONDITIONAL([WANT_INSTALL_SOURCES],
  [test "x$vicare_enable_INSTALL_SOURCES" = xyes])

VICARE_ENABLE_OPTION([TIME_TESTS],[time-tests],[no],
  [whether to enable timing of tests],
  [enable timing of tests])
AM_CONDITIONAL([WANT_TIME_TESTS],[test "x$vicare_enable_TIME_TESTS" = xyes])

AM_COND_IF([WANT_TIME_TESTS],
  [AC_PATH_PROG([TIME_PROGRAM],[time])])

#page
#### expat configuration

AC_CHECK_HEADERS([expat.h],,
  [AC_MSG_FAILURE([missing expat.h header],[2])])
AC_CHECK_LIB([expat],[XML_ParserCreate],,
  [AC_MSG_FAILURE([missing Expat library],[2])])

VICARE_EXTENSION_LIBRARY_VERSION([expat],[0],[0],[0])

#page
#### symbols

m4_define([VICARE_INCLUDES],[
AC_INCLUDES_DEFAULT
#ifdef HAVE_VICARE_H
#  include <vicare.h>
#endif
#ifdef HAVE_EXPAT_H
#  include <expat.h>
#endif
])

dnl Preprocessor symbols: XML_Bool
VICARE_CONSTANT_TESTS([XML_TRUE XML_FALSE])

dnl enum XML_Status
VICARE_CONSTANT_TESTS([XML_STATUS_ERROR XML_STATUS_OK XML_STATUS_SUSPENDED])

dnl enum XML_Error
VICARE_CONSTANT_TEST([XML_ERROR_NONE])
VICARE_CONSTANT_TEST([XML_ERROR_NO_MEMORY])
VICARE_CONSTANT_TEST([XML_ERROR_SYNTAX])
VICARE_CONSTANT_TEST([XML_ERROR_NO_ELEMENTS])
VICARE_CONSTANT_TEST([XML_ERROR_INVALID_TOKEN])
VICARE_CONSTANT_TEST([XML_ERROR_UNCLOSED_TOKEN])
VICARE_CONSTANT_TEST([XML_ERROR_PARTIAL_CHAR])
VICARE_CONSTANT_TEST([XML_ERROR_TAG_MISMATCH])
VICARE_CONSTANT_TEST([XML_ERROR_DUPLICATE_ATTRIBUTE])
VICARE_CONSTANT_TEST([XML_ERROR_JUNK_AFTER_DOC_ELEMENT])
VICARE_CONSTANT_TEST([XML_ERROR_PARAM_ENTITY_REF])
VICARE_CONSTANT_TEST([XML_ERROR_UNDEFINED_ENTITY])
VICARE_CONSTANT_TEST([XML_ERROR_RECURSIVE_ENTITY_REF])
VICARE_CONSTANT_TEST([XML_ERROR_ASYNC_ENTITY])
VICARE_CONSTANT_TEST([XML_ERROR_BAD_CHAR_REF])
VICARE_CONSTANT_TEST([XML_ERROR_BINARY_ENTITY_REF])
VICARE_CONSTANT_TEST([XML_ERROR_ATTRIBUTE_EXTERNAL_ENTITY_REF])
VICARE_CONSTANT_TEST([XML_ERROR_MISPLACED_XML_PI])
VICARE_CONSTANT_TEST([XML_ERROR_UNKNOWN_ENCODING])
VICARE_CONSTANT_TEST([XML_ERROR_INCORRECT_ENCODING])
VICARE_CONSTANT_TEST([XML_ERROR_UNCLOSED_CDATA_SECTION])
VICARE_CONSTANT_TEST([XML_ERROR_EXTERNAL_ENTITY_HANDLING])
VICARE_CONSTANT_TEST([XML_ERROR_NOT_STANDALONE])
VICARE_CONSTANT_TEST([XML_ERROR_UNEXPECTED_STATE])
VICARE_CONSTANT_TEST([XML_ERROR_ENTITY_DECLARED_IN_PE])
VICARE_CONSTANT_TEST([XML_ERROR_FEATURE_REQUIRES_XML_DTD])
VICARE_CONSTANT_TEST([XML_ERROR_CANT_CHANGE_FEATURE_ONCE_PARSING])
VICARE_CONSTANT_TEST([XML_ERROR_UNBOUND_PREFIX])
VICARE_CONSTANT_TEST([XML_ERROR_UNDECLARING_PREFIX])
VICARE_CONSTANT_TEST([XML_ERROR_INCOMPLETE_PE])
VICARE_CONSTANT_TEST([XML_ERROR_XML_DECL])
VICARE_CONSTANT_TEST([XML_ERROR_TEXT_DECL])
VICARE_CONSTANT_TEST([XML_ERROR_PUBLICID])
VICARE_CONSTANT_TEST([XML_ERROR_SUSPENDED])
VICARE_CONSTANT_TEST([XML_ERROR_NOT_SUSPENDED])
VICARE_CONSTANT_TEST([XML_ERROR_ABORTED])
VICARE_CONSTANT_TEST([XML_ERROR_FINISHED])
VICARE_CONSTANT_TEST([XML_ERROR_SUSPEND_PE])
VICARE_CONSTANT_TEST([XML_ERROR_RESERVED_PREFIX_XML])
VICARE_CONSTANT_TEST([XML_ERROR_RESERVED_PREFIX_XMLNS])
VICARE_CONSTANT_TEST([XML_ERROR_RESERVED_NAMESPACE_URI])

dnl enum XML_Content_Type
VICARE_CONSTANT_TESTS([XML_CTYPE_EMPTY XML_CTYPE_ANY XML_CTYPE_MIXED XML_CTYPE_NAME XML_CTYPE_CHOICE XML_CTYPE_SEQ])

dnl enum XML_Content_Quant
VICARE_CONSTANT_TESTS([XML_CQUANT_NONE XML_CQUANT_OPT XML_CQUANT_REP XML_CQUANT_PLUS])

dnl enum XML_Parsing
VICARE_CONSTANT_TESTS([XML_INITIALIZED XML_PARSING XML_FINISHED XML_SUSPENDED])

dnl enum XML_ParamEntityParsing
VICARE_CONSTANT_TESTS([XML_PARAM_ENTITY_PARSING_NEVER XML_PARAM_ENTITY_PARSING_UNLESS_STANDALONE XML_PARAM_ENTITY_PARSING_ALWAYS])

dnl Preprocessor symbols: version numbers
VICARE_CONSTANT_TESTS([XML_MAJOR_VERSION XML_MINOR_VERSION XML_MICRO_VERSION])

dnl enum XML_FeatureEnum
VICARE_CONSTANT_TEST([XML_FEATURE_END])
VICARE_CONSTANT_TEST([XML_FEATURE_UNICODE])
VICARE_CONSTANT_TEST([XML_FEATURE_UNICODE_WCHAR_T])
VICARE_CONSTANT_TEST([XML_FEATURE_DTD])
VICARE_CONSTANT_TEST([XML_FEATURE_CONTEXT_BYTES])
VICARE_CONSTANT_TEST([XML_FEATURE_MIN_SIZE])
VICARE_CONSTANT_TEST([XML_FEATURE_SIZEOF_XML_CHAR])
VICARE_CONSTANT_TEST([XML_FEATURE_SIZEOF_XML_LCHAR])
VICARE_CONSTANT_TEST([XML_FEATURE_NS])
VICARE_CONSTANT_TEST([XML_FEATURE_LARGE_SIZE])

#page
#### finish

VICARE_OUTPUT
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([Makefile]
   [lib/vicare/xml/expat/constants.vicare.sls])
AC_OUTPUT

### end of file
# Local Variables:
# mode: autoconf
# page-delimiter: "^#page"
# End:
